buildscript {

    apply from: "config.gradle"
    apply from: "maven.gradle"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath libs.tools.gradle
        classpath libs.kotlin.gradle
        classpath libs.cpd
        classpath libs.detekt.gradle
        classpath libs.ktlint
        classpath libs.dokka
        classpath libs.protobuf.gradle
        classpath libs.kover
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

apply from: "deploy.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}

task lintAll(dependsOn: [
        ':dbinspector:lintRelease',
        ':dbinspector-no-op:lintRelease',
        ':dbinspector-web:lintRelease'
]) {
    group = "Verification"
    description = "Run Lint on all modules"
}

task detektAll(dependsOn: [
        ':dbinspector:detekt',
        ':dbinspector-no-op:detekt',
        ':dbinspector-web:detekt'
]) {
    group = "Verification"
    description = "Run Detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':dbinspector:ktlintCheck',
        ':dbinspector-no-op:ktlintCheck',
        ':dbinspector-web:ktlintCheck'
]) {
    group = "Verification"
    description = "Run Ktlint on all modules"
}

task cpdAll(dependsOn: [
        ':dbinspector:cpdCheck',
        ':dbinspector-no-op:cpdCheck',
        ':dbinspector-web:cpdCheck'
]) {
    group = "Verification"
    description = "Run CPD on all modules"
}

task runStaticChecks(dependsOn: [
        ':lintAll',
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
]) {
    group = "Verification"
    description = "Run static checks on all modules"
}

task cleanMavenLocal {
    doLast {
        new File('~/.m2/repository/example').deleteDir()
    }
}
